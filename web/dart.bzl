# Copyright 2016 Google Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
"""Web Test rules for Dart."""

load("@io_bazel_rules_dart//dart/build_rules:vm.bzl", "dart_vm_test")
load("//web:web.bzl", "web_test_suite")


def dart_web_test_suite(name,
                        browsers,
                        args=None,
                        browser_overrides=None,
                        config=None,
                        dart_test_tags=("manual",),
                        flaky=None,
                        local=None,
                        script_file=None,
                        shard_count=None,
                        size="large",
                        tags=None,
                        test_suite_tags=("manual",),
                        timeout=None,
                        visibility=None,
                        web_test_data=None,
                        **remaining_keyword_args):
  """Defines a test_suite of web_test targets that wrap a dart_vm_test target.

  Any attribute for dart_vm_test is supported by dart_web_test_suite. If the
  argument isn't listed below, then the argument values have the same
  exact meaning as in dart_vm_test.

  Args:
    name: The base name of the test.
    browsers: A sequence of lavels specifying the browsers to use.
    args: Args for web_test targets generated by this extension.
    browser_overrides: Dictionary mapping from browsers to browser-specific
      attributes, such as shard_count, flakiness, timeout, etc. For example:
      {'chrome-linux': {'shard_count': 3, 'flaky': 1}
       'firefox-linux': {'shard_count': 1, 'size': 'medium', 'timeout': 100}}
      (default: an empty dictionary)
    config: Label; optional; Configuration of web test features.
    dart_test_tags: A list of test tag strings to use for the dart_test target.
      Defaults to ['manual'].
    flaky: A boolean specifying that the test is flaky. If set, the test will
      be retried up to 3 times (default: 0)
    local: boolean; optional.
    script_file: The name of the source file that is the main entry point of the
      application. (default: computed from name)
    shard_count: The number of test shards to use per browser. (default: 1)
    size: A string specifying the test size. (default: 'large')
    tags: A list of test tag strings to apply to each generated web_test target.
    test_suite_tags: A list of tag strings for the generated test_suite.
    timeout: A string specifying the test timeout (default: computed from size)
    visibility: List of labels; optional.
    web_test_data: Data dependencies for the web_test.
    **remaining_keyword_args: Arguments for dart_vm_test
  """
  dart_test_name = name + "_wrapped_test"
  if script_file == None:
    script_file = "test/" + name + ".dart"

  dart_vm_test(
      name=dart_test_name,
      args=args,
      flaky=flaky,
      local=local,
      script_file=script_file,
      shard_count=shard_count,
      size=size,
      tags=dart_test_tags,
      timeout=timeout,
      visibility=["//visibility:private"],
      **remaining_keyword_args)

  web_test_suite(
      name=name,
      args=args,
      browsers=browsers,
      browser_overrides=browser_overrides,
      config=config,
      data=web_test_data,
      flaky=flaky,
      local=local,
      shard_count=shard_count,
      size=size,
      tags=tags,
      test=dart_test_name,
      test_suite_tags=test_suite_tags,
      timeout=timeout,
      visibility=visibility)
